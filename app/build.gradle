apply plugin: 'com.android.application'


tasks.matching { it instanceof Test }.all {
    testLogging.events = ["failed", "passed", "skipped"]
}

def final yourApplicationId = 'com.aayaffe.sailingracecoursemanager'

sonarqube {
    properties {
        property "sonar.projectName", "SailingRaceCourseManager"
        property "sonar.projectKey", "AVI"
        //property "sonar.exclusions","build/**,**/*.png"
        //property "sonar.import_unknown_files", true
        //property "sonar.android.lint.report", "./build/outputs/lint-results.xml"
    }
}


android {
    signingConfigs {
        if (!System.getenv("TRAVIS")) {
            release {
                // Load keystore
                def keystorePropertiesFile = rootProject.file("keystore.properties");
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
                storeFile = file(keystoreProperties['storeFile'])
                storePassword = keystoreProperties['storePassword']
                keyAlias = keystoreProperties['keyAlias']
                keyPassword = keystoreProperties['keyPassword']
            }
        }
//        else{
//            environment "AVI_VER", "1.75"
//        }
    }


    compileSdkVersion 26
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId = yourApplicationId
        minSdkVersion 18
        targetSdkVersion 26
        versionCode 35
        versionName "1.75"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        resConfigs "en"
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (!System.getenv("TRAVIS")) {
                signingConfig signingConfigs.release
            }
        }
        staging {
            debuggable true
            jniDebuggable false
            if (!System.getenv("TRAVIS")) {
                signingConfig signingConfigs.release
            }
            renderscriptDebuggable false
            minifyEnabled false
            zipAlignEnabled true
        }
        debuggableRelease {
            debuggable true
            jniDebuggable false
            if (!System.getenv("TRAVIS")) {
                signingConfig signingConfigs.release
            }
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/DEPENDENCIES'
    }
    testOptions {
        unitTests.returnDefaultValues = true

        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //androidTestImplementation 'com.android.support:customtabs:25.3.1'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.android.support:customtabs:26.1.0'
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support:cardview-v7:26.1.0'
    implementation "com.android.support:support-compat:26.1.0"
    implementation 'com.google.android.gms:play-services-location:15.0.1'
    implementation 'com.google.android.gms:play-services-auth:15.0.1'
    implementation 'com.google.android.gms:play-services-maps:15.0.1'
    implementation 'com.google.firebase:firebase-database:16.0.1'
    implementation 'com.google.firebase:firebase-auth:16.0.2'
    implementation 'com.google.firebase:firebase-core:16.0.1'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.4'
    implementation 'com.firebaseui:firebase-ui-database:4.0.1'
    implementation 'com.firebaseui:firebase-ui-auth:4.0.1'
    implementation 'com.google.guava:guava:22.0'
    implementation 'org.jetbrains:annotations-java5:15.0'
    implementation 'com.borax12.materialdaterangepicker:library:1.9'
    implementation 'com.tenmiles:helpstack:1.2.0'
    implementation 'io.doorbell:android-sdk:0.2.8@aar'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    testImplementation 'org.robolectric:robolectric:3.2.2'
    androidTestImplementation 'com.android.support:support-annotations:26.1.0'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
}

apply plugin: 'com.google.gms.google-services'
