apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'


tasks.matching { it instanceof Test }.all {
    testLogging.events = ["failed", "passed", "skipped"]
}

def final yourApplicationId = 'com.aayaffe.sailingracecoursemanager'

android {
    signingConfigs {
        if (!System.getenv("TRAVIS")) {
            release {
                // Load keystore
                def keystorePropertiesFile = rootProject.file("keystore.properties");
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
                storeFile = file(keystoreProperties['storeFile'])
                storePassword = keystoreProperties['storePassword']
                keyAlias = keystoreProperties['keyAlias']
                keyPassword = keystoreProperties['keyPassword']
            }
        }
    }


    compileSdkVersion 33
    defaultConfig {
        applicationId = yourApplicationId
        minSdkVersion 19
        targetSdkVersion 33
        versionCode 40
        versionName "1.81"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        multiDexEnabled true
        resourceConfigurations += ['en']
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
            applicationIdSuffix ".dev"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (!System.getenv("TRAVIS")) {
                signingConfig signingConfigs.release
            }
        }
        staging {
            debuggable true
            jniDebuggable false
            if (!System.getenv("TRAVIS")) {
                signingConfig signingConfigs.release
            }
            renderscriptDebuggable false
            minifyEnabled false
            zipAlignEnabled true
        }
        debuggableRelease {
            debuggable true
            jniDebuggable false
            if (!System.getenv("TRAVIS")) {
                signingConfig signingConfigs.release
            }
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES.txt', 'META-INF/LICENSE', 'META-INF/LICENSE-FIREBASE.txt', 'META-INF/NOTICE', 'META-INF/DEPENDENCIES']
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true

        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
                excludes = ['jdk.internal.*']
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    namespace 'com.aayaffe.sailingracecoursemanager'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(":OrcCertificates")
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.browser:browser:1.5.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.core:core:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.gms:play-services-location:18.0.0'
    implementation 'com.google.android.gms:play-services-auth:20.5.0'
    implementation 'com.google.android.gms:play-services-maps:18.1.0'
    implementation platform('com.google.firebase:firebase-bom:32.0.0')
    implementation 'com.google.firebase:firebase-database'
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-config'
    implementation 'com.google.firebase:firebase-firestore-ktx'
    implementation 'com.firebaseui:firebase-ui-database:7.1.1'
    implementation 'com.firebaseui:firebase-ui-auth:7.2.0'
    implementation 'com.google.guava:guava:31.1-android'
    implementation 'org.jetbrains:annotations:16.0.2'
    implementation 'com.borax12.materialdaterangepicker:library:2.0'
    implementation 'io.doorbell:android-sdk:0.4.3@aar'
    implementation 'org.jetbrains:annotations:16.0.2'
    implementation 'androidx.core:core-ktx:1.10.1'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:2.19.0'
    testImplementation 'org.robolectric:robolectric:4.10'
    androidTestImplementation 'androidx.annotation:annotation:1.6.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'org.jetbrains.kotlin.android'
